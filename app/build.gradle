apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.aglhz.yicommunity"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.gitVersionCode()
        versionName rootProject.ext.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            keyAlias 'agl'
            keyPassword 'aglandroid'
            storeFile file('../agl.keystore')
            storePassword 'aglandroid'
        }
        release {
            keyAlias 'agl'
            keyPassword 'aglandroid'
            storeFile file('../agl.keystore')
            storePassword 'aglandroid'
        }
    }

    buildTypes {
        debug {
            ndk {
                abiFilters "armeabi"
            }
            signingConfig signingConfigs.debug
            aaptOptions {
                cruncherEnabled false//快速处理PNG图片
            }
        }
        release {
            ndk {
                abiFilters "armeabi"
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"//这个名字貌似随便取，也可以有多个，总之一定要有..
    productFlavors {
        yishequ {
            resValue "string", "app_name", "亿社区"
            applicationId 'com.aglhz.yicommunity'
        }
        meilun {
            resValue "string", "app_name", "美好生活"
            applicationId 'com.meilun.one'
        }
        vensi {
            resValue "string", "app_name", "威士丹力"
            applicationId 'com.aglhz.yicommunity'
        }
    }

    // 输出文件配置，格式如下：app-{渠道名称+版本}-{版本号}-{日期时间}.apk
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //app-yishequDebug-2.2.5-201803101650
                outputFileName = "app-${variant.name}-${variant.versionCode}-${new Date().format('yyyyMMddHHmm')}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    //阿里巴巴开源基础路由
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    implementation project(':amain')
}
